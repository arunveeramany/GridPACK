\chapter{Fortran 2003 Interface}

GridPACK has developed a Fortran interface that can be used to access most of the functionality in the framework modules. The Fortran interface makes extensive use of the object-oriented features in Fortran, so a compiler that supports the Fortran 2003 standard must be used if creating Fortran applications. The Fortran compiler must also support the iso\_c\_binding module, but this will usually be available if the compiler supports Fortran 2003. Most recent compilers support Fortran 2003. A working power flow application written entirely in Fortran has been included in the current release and demonstrates how to use the Fortran interface. The Fortran implementation is very similar to the C++ interface and most of the C++ documentation applies to the corresponding Fortran functionality. The remainder of this section will highlight the important differences between the C++ and Fortran interfaces.

Because Fortran does not have any support for templates (that we know of), the Fortran interface cannot support multiple different kinds of networks within a single application. This means that only one bus and one branch class can be present in an application, so the bus and branch classes must support all possible types of behavior. It is still possible to have more than one network in an application, but all networks must be of the same type.

The bus and branch classes in the Fortran interface are represented by the Fortran derived types \texttt{\textbf{application\_bus}} and \texttt{\textbf{application\_branch}}. These types have procedures bound to them, as well as internal data elements. These types are defined in the Fortran file \texttt{\textbf{component\_template.F90}} file that is located in the \texttt{\textbf{fortran/component}} directory. The application bus and branch classes can be created by modifying a copy of \texttt{\textbf{component\_template.F90}}. The functions in the math-vector interface and the component base classes are all defined in this file, along with default implementations for these functions. Additional data elements and procedures can be added to the bus and branch data types to create appropriate functionality for specific problems.

A brief overview of the \texttt{\textbf{application\_bus}} type in the \texttt{\textbf{component\_template.F90}} file is provided here. Similar considerations apply to the \texttt{\textbf{application\_branch}} type. To use the \texttt{\textbf{component\_template.F90}} file it should first be copied to the directory where the application source code resides and renamed to something appropriate. We will use the name \texttt{\textbf{app\_component.F90}}. Inside the component file, the Fortran types \texttt{\textbf{bus\_xc\_data}}, \texttt{\textbf{branch\_xc\_data}}, \texttt{\textbf{application\_bus}}, \texttt{\textbf{application\_branch}} are defined as part of the \texttt{\textbf{application\_components}} module. These are the only types that need concern the application developer. There are also two types defined in this file called \texttt{\textbf{application\_bus\_wrapper}} and \texttt{\textbf{application\_branch\_wrapper}}. These are only used internally but must be defined in this file. They should not be modified. There is a line at the bottom of the \texttt{\textbf{app\_component.F90}} file that includes an external file \texttt{\textbf{component\_inc.F90}}. This file contains many functions that are required by the interface and must be included in the \texttt{\textbf{application\_components}} module. However, these functions should not be modified by the user so to avoid possible errors and to simplify the file somewhat, these functions are put in an include file.

The \texttt{\textbf{application\_bus}} type has four parts. These consist of 1) application-specific data elements, 2) data elements that must be defined in order for the component to interact with rest of the framework, 3) application-specific functions that are defined by the user and 4) framework functions that must be included in the component. The framework functions all have base implementations can be modified to suit the application. The only data elements that must be included in the \texttt{\textbf{application\_bus}} type is a variable of type \texttt{\textbf{bus\_xc\_data}} and a pointer to this variable. The \texttt{\textbf{bus\_xc\_data}} type will be discussed further below and represents all data that might need to be exchanged in a bus update.

The framework functions are directly analogous to the functions defined for the C++ implementation and users should refer to the documentation above to find out how these functions work. This section will primarily discuss differences between the Fortran and C++ interfaces. The Fortran compilers do not have the same name-mangling capabilities as C++ so all function names are preceded by either a \texttt{\textbf{bus\_}} or \texttt{\textbf{branch\_}} to distinguish between bus and branch versions of the functions. A few functions only appear in the bus class or the branch class and do not necessarily need this prefix, but to be consistent, this convention is used for all functions.

Functions that are bound to the \texttt{\textbf{application\_bus}} type are already listed in the \texttt{\textbf{component\_template.F90}}. These functions consist of both a declaration within the \texttt{\textbf{application\_bus}} type and a function or subroutine implementation within the \texttt{\textbf{application\_components}} module. The declarations within the \texttt{\textbf{application\_bus}} type (after the \texttt{\textbf{contains}} keyword) have the form

{
\color{red}
\begin{Verbatim}[fontseries=b]
    procedure::bus_matrix_diag_size
    procedure::bus_matrix_diag_values
    procedure::bus_matrix_forward_size
    procedure::bus_matrix_reverse_size
         :
\end{Verbatim}
}

The \texttt{\textbf{procedure}} keyword distinguishes a function or subroutine bound to the Fortran type from a piece of data (which is declared as a data type using one of the intrinsic Fortran data types or a Fortran type declaration).
After the type declarations within the \texttt{\textbf{applications\_components}} module, there is a \texttt{\textbf{contains}} keyword followed by the subroutine and function implementations for all the declared procedures. The original implementations in the \texttt{\textbf{component\_template.F90}} file are just stubs for these functions and typically don't do much. An example is the \texttt{\textbf{bus\_matrix\_diag\_size}} function which originally has the implementation

{
\color{red}
\begin{Verbatim}[fontseries=b]
  logical function bus_matrix_diag_size(bus, isize, jsize)
    implicit none
    class(application_bus), intent(in) :: bus
    integer, intent(out) :: isize, jsize
    bus_matrix_diag_size = .false.
    return
  end function bus_matrix_diag_size
\end{Verbatim}
}

The initial implementation just returns false if this function is invoked and doesn't set the variables \texttt{\textbf{isize}} or \texttt{\textbf{jsize}}. Note the first item in the argument list. This is declared as being of type \texttt{\textbf{class(application\_bus)}} with intent in. All functions and subroutines that are bound to the \texttt{\textbf{application\_bus}} type must have this argument, even if they do not have any other arguments. This argument provides a mechanism for accessing data items or functions that are related to a particular \texttt{\textbf{application\_bus}} instance.

To see how the bus argument is used in actual practice, an implementation of this function in a power flow application is shown below

{
\color{red}
\begin{Verbatim}[fontseries=b]
  logical function bus_matrix_diag_size(bus, isize, jsize)
    implicit none
    class(application_bus), intent(in) :: bus
    integer, intent(out) :: isize, jsize
    isize = 1
    jsize = 1
    bus_matrix_diag_size = .true.
    if (bus%p_mode.eq.JACOBIAN) then
      if (.not.bus%bus_is_isolated()) then
        isize = 2
        jsize = 2
        bus_matrix_diag_size = .true.
      else
        bus_matrix_diag_size = .false.
      endif
    else if (bus%p_mode.eq.YBUS) then
      if (.not.bus%bus_is_isolated()) then
        bus_matrix_diag_size = .true.
        isize = 1
        jsize = 1
      else
        bus_matrix_diag_size = .false.
      endif
      return
    endif
    return
  end function bus_matrix_diag_size
\end{Verbatim}
}

The \texttt{\textbf{application\_bus}} implementation for power flow contains the variable \texttt{\textbf{p\_mode}} and a user-specified function \texttt{\textbf{bus\_is\_isolated}} (this is declared as a type-bound procedure). To access this data and this function inside a type-bound procedure, use the Fortran ``\texttt{\textbf{\%}}'' symbol. The \texttt{\textbf{bus}} variable in the argument list is acting in a similar way to the ``\texttt{\textbf{this}}'' pointer in C++ and refers back to the \texttt{\textbf{application\_bus}} instance that made the original call to \texttt{\textbf{bus\_matrix\_diag\_size}}. Although the \texttt{\textbf{bus\_is\_isolated}} function implementation has the variable \texttt{\textbf{bus}} in its argument list, it doesn't need to explicitly pass this argument when making a call from an \texttt{\textbf{application\_bus}} instance. The \texttt{\textbf{bus}} argument is assumed in this case. Similarly, a call to the \texttt{\textbf{bus\_matrix\_diag\_size}} function, which has additional arguments, would have the form

{
\color{red}
\begin{Verbatim}[fontseries=b]
      ok = bus%bus_matrix_diag_size(isize,jsize)
\end{Verbatim}
}

Following this syntax, it is possible to construct a complete set of functions for an arbitrary application. Additional application-specific functions can be added to the component types by declaring them as procedures within the type and adding their implementations to the \texttt{\textbf{application\_components}} module.

There are a few procedures in both the bus and branch types that should not be modified. No stubs for these appear in the component\_template.F90 file. For the application\_bus type, these procedures are

{
\color{red}
\begin{Verbatim}[fontseries=b]
    procedure::bus_get_neighbor_branch
    procedure::bus_get_neighbor_bus
    procedure::bus_get_xc_buf_size
    procedure::bus_get_xc_buf
\end{Verbatim}
}

For the application\_branch type, the procedures are

{
\color{red}
\begin{Verbatim}[fontseries=b]
    procedure::branch_get_bus1
    procedure::branch_get_bus2
    procedure::branch_get_xc_buf_size
    procedure::branch_get_xc_buf
\end{Verbatim}
}

These procedures are required by other parts of the framework, but should not be modified by the user. Some other procedures are defined in the base class and do not appear as procedure declarations in application\_bus and application\_branch types. These procedures include

{
\color{red}
\begin{Verbatim}[fontseries=b]
    procedure::bus_get_num_neighbors
    procedure::bus_set_reference_bus
    procedure::bus_get_reference_bus
    procedure::bus_get_original_index
    procedure::bus_compare
\end{Verbatim}
}

for buses and

{
\color{red}
\begin{Verbatim}[fontseries=b]
    procedure::branch_get_bus1_original_index
    procedure::branch_get_bus2_original_index
    procedure::branch_compare
\end{Verbatim}
}

for branches. The bus and branch compare functions are used to determine if a bus or branch is equal to another bus or branch. An example of how to use this function can be found in the function that evaluates transformer contributions on branches for the power flow application. The syntax for calling this function is

{
\color{red}
\begin{Verbatim}[fontseries=b]
double complex function branch_get_transformer(branch, bus)
    :
  class(application_branch), intent(in) :: branch
  class(application_bus), intent(in) :: bus
  class(application_bus), pointer :: bus1, bus2
    :
  if (bus%bus_compare(bus1)) then
    :
\end{Verbatim}
}

In this fragment, the \texttt{\textbf{bus\_compare}} function is being used to check if bus1 is equivalent to bus. The \texttt{\textbf{branch\_compare}} function is used in a similar way.

The final issue in implementing the Fortran application bus and branch classes is understanding the exchange buffers. These buffers are declared at the top of the \texttt{\textbf{component\_template.F90}} file as the \texttt{\textbf{bus\_xc\_data}} and \texttt{\textbf{branch\_xc\_data}} data types. Although the underlying Fortran interface implementation makes extensive use of the \texttt{\textbf{iso\_c\_binding}} module, we have worked very hard to keep the \texttt{\textbf{iso\_c\_binding}} data types out of the Fortran interface itself. However, the one place where this is not possible is in the exchange buffers, so it is important to use these data type declarations for any variables that are included in the exchange buffers. The exchange buffers are declared as follows in the top of the \texttt{\textbf{component\_template.F90}} file

{
\color{red}
\begin{Verbatim}[fontseries=b]
  type, bind(c), public :: bus_xc_data
!
!  Example data types. Replace with application-specific values
!
    integer(C_INT) int_reg
    integer(C_LONG) int_long
    real(C_FLOAT) real_s
    real(C_DOUBLE) real_d
    complex(C_FLOAT_COMPLEX) complex_s
    complex(C_DOUBLE_COMPLEX) complex_d
    logical(C_BOOL) log_reg
  end type
\end{Verbatim}
}

The variables \texttt{\textbf{int\_reg}}, \texttt{\textbf{int\_long}}, \texttt{\textbf{real\_s}}, \texttt{\textbf{real\_d}}, \texttt{\textbf{complex\_s}}, \texttt{\textbf{complex\_d}} and \texttt{\textbf{log\_reg}} are just examples and should be replaced with the variables used in the actual application. Not all data types will be used in an application. Any buffer variables used in an application should use the

 \texttt{\textbf{iso\_c\_binding}} type declarations (\texttt{\textbf{C\_INT}}, \texttt{\textbf{C\_LONG}}, \texttt{\textbf{C\_FLOAT}}, \texttt{\textbf{C\_FLOAT\_COMPLEX}}, \texttt{\textbf{C\_DOUBLE\_COMPLEX}}, \texttt{\textbf{C\_BOOL}}). Variables declared with the \texttt{\textbf{iso\_c\_binding}} types can be cast to regular Fortran variables by relying on the compiler to automatically cast an assignment to the right sized variable. For example

{
\color{red}
\begin{Verbatim}[fontseries=b]
  integer f_var
  integer(C_INT) c_var
     :
  f_var = c_var
\end{Verbatim}
}

If \texttt{\textbf{f\_var}} is an 8 byte integer and \texttt{\textbf{c\_var}} is a 4 byte integer, the compiler can be relied on to do the cast. This also works in the opposite direction, assuming that \texttt{\textbf{f\_var}} does not exceed the capacity of a 4 byte variable.

The functions that access neighboring branches or buses also work differently than the corresponding C++ functions. Fortran does not support anything that looks like an STL vector so neighbors are accessed from buses using a two step process. The first step is to get the total number of neighbors attached to the bus using the \texttt{\textbf{bus\_get\_num\_neighbors}} procedure. This allows users to set up a loop that can be used to run over either the neighboring branches or the neighboring buses that are attached to the calling bus via a single branch. The neighboring branches can then be accessed by using the bus\_get\_neighbor\_branch function which returns a Fortran pointer to the neighboring branch. The syntax for using this function is

{
\color{red}
\begin{Verbatim}[fontseries=b]
  integer i, nbranch
  type(application_branch), pointer :: branch
  nbranch = bus%bus_get_num_neighbors()
  do i = 1, nbranch
    branch => bus%bus_get_neighbor_branch(i)
       :
\end{Verbatim}
}

The \texttt{\textbf{bus\_get\_neighbor\_bus}} function works in a similar way and returns a pointer to the bus at the other end of branch \texttt{\textbf{i}}. To get pointers to the buses at either end of a branch, use the functions \texttt{\textbf{branch\_get\_bus1}} and \texttt{\textbf{branch\_get\_bus2}} procedures. Because the Fortran interface only supports one type of bus or branch per application, these functions return pointers of the correct type and there is no need to cast them to something else.

Most of the remaining differences between the Fortran and C++ interfaces are associated with the GridPACK factory module. As with the component classes, the Fortran interface only supports one kind of factory. This is the \texttt{\textbf{app\_factory}} type and it can be created by copying the \texttt{\textbf{factory\_template.F90}} file in the \texttt{\textbf{fortran/factory}} directory and making application-specific changes to it. The factory base class contains the functions

{
\color{red}
\begin{Verbatim}[fontseries=b]
    procedure::set_components
    procedure::load
    procedure::set_exchange
    procedure::set_mode
    procedure::check_true
\end{Verbatim}
}

These functions behave the same way as the equivalent C++ functions. In addition, the \texttt{\textbf{app\_factory}} type contains the two functions

{
\color{red}
\begin{Verbatim}[fontseries=b]
    procedure::create
    procedure::destroy
\end{Verbatim}
}

Because Fortran does not support constructors and destructors in the same way as C++, it is necessary to create explicit functions that implement whatever behaviors are imbedded in the C++ constructors and destructors. This is accomplished in the Fortran interface by adding \texttt{\textbf{create}} and \texttt{\textbf{destroy}} functions (or \texttt{\textbf{initialize}} and \texttt{\textbf{finalize}} functions) to most of the Fortran implementations of the GridPACK modules.

Additional methods can be added to the \texttt{\textbf{app\_factory}} type to support application-specific functionality. An example of how to do this is the \texttt{\textbf{set\_y\_bus}} procedure for the power flow application. This subroutine is declared as a procedure in the \texttt{\textbf{app\_factory}} type. The implementation is written as

{
\color{red}
\begin{Verbatim}[fontseries=b]
  subroutine set_y_bus(factory)
    class(app_factory), intent(in) :: factory
    class(application_bus), pointer :: bus
    class(application_branch), pointer :: branch
    class(network), pointer :: grid
    integer nbus, nbranch, i
    grid => factory%p_network_int
    nbus = grid%num_buses()
    nbranch = grid%num_branches()
    do i = 1, nbus
      bus => bus_cast(grid%get_bus(i))
      call bus%bus_set_y_matrix()
    end do
    do i = 1, nbranch
      branch => branch_cast(grid%get_branch(i))
      call branch%branch_set_y_matrix()
    end do
    return
  end subroutine set_y_bus
\end{Verbatim}
}

The functions for accessing the bus and branch objects work differently from the functions that get neighboring branches or buses in the component classes. The neighbor bus and branch functions return a pointer to the appropriate bus or branch directly to the calling application. The \texttt{\textbf{get\_bus}} and \texttt{\textbf{get\_branch}} functions in the Fortran network class return an opaque object that cannot be directly used in a Fortran code. To convert this to  a bus or branch pointer it is necessary to call the \texttt{\textbf{bus\_cast}} or \texttt{\textbf{branch\_cast}} functions which return a pointer that can be called in Fortran.

The last remaining point is to provide a list of the existing Fortran modules that need to be used in a GridPACK application using the Fortran interface. These modules need to be included in any subroutine or function that is using the associated Fortran types. The existing modules are

{
\color{red}
\begin{Verbatim}[fontseries=b]
  gridpack_network ! type or class network
  application_factory ! type or class app_factory
  application_components ! type or class application_bus and
                         ! application_branch
  gridpack_configuration ! type or class cursor
  gridpack_full_matrix_map ! type or class full_matrix_map
  gridpack_bus_vector_map ! type or class bus_vector_map
  gridpack_gen_matrix_map ! type or class gen_matrix_map
  gridpack_gen_vector_map ! type or class gen_vector_map
  gridpack_math ! access to math initialization and
                ! finalization routines
  gridpack_matrix ! type or class matrix
  gridpack_vector ! type or class vector
  gridpack_linear_solver ! type or class linear_solver
  gridpack_nonlinear_solver ! type or class funcbuilder
                            ! and nonlinear_solver
  gridpack_communicator ! type or class communicator
  gridpack_parallel ! access to parallel initialization
                    ! and finalization routines
  gridpack_parser ! class or type pti23_parser
  gridpack_serial_io ! class or type bus_serial_io
                     ! and branch_serial_io
\end{Verbatim}
}

The appropriate module should be included in any function or subroutine that uses objects defined in the module. Modules can be included using the standard Fortran ``\texttt{\textbf{use}}'' statement.
